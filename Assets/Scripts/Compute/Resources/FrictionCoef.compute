#pragma kernel CSMain
#pragma kernel ToCoefficient

RWStructuredBuffer<float4> Result;
RWStructuredBuffer<float4> frictionCoef;

Texture2D<float4> image;

float3 Dispatch;
float collisions;
int subtextureSize;
float nominal_friction;
float kA, kB;

groupshared float4 TotalSum[256];

[numthreads(16,16,1)]
void CSMain(uint3 groupID : SV_GroupID, uint3 dispatchID : SV_DispatchThreadID, uint groupIndex : SV_GroupIndex)
{
    TotalSum[groupIndex] = image[dispatchID.xy];
    GroupMemoryBarrierWithGroupSync();
    
    for (uint k = 128; k > 0; k >>= 1)
    {
        if (groupIndex < k)
        {
            TotalSum[groupIndex] += TotalSum[groupIndex + k];
        }
        GroupMemoryBarrierWithGroupSync();
    }
    
    if (groupIndex == 0)
    {
        int index = groupID.x + groupID.y * Dispatch.x;
        Result[index] = TotalSum[0];
    }
    
}

[numthreads(64,1,1)]
void ToCoefficient(uint3 dispatchID : SV_DispatchThreadID)
{
    uint i = dispatchID.x;
    if(i >= collisions)
        return;

    float4 mu1 = Result[2 * i];
    float4 mu2 = Result[2 * i + 1];
    float validPixels = subtextureSize * subtextureSize;

    mu1 /= validPixels;
    mu2 /= validPixels;

    frictionCoef[i] = kA * mu1 + kB * mu2;
    frictionCoef[i] += nominal_friction;
}